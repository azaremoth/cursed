#include "basic_spring_stuff.h"

piece  base, building, platform, createpoint, emit_blight1, emit_blight2, emit_rlight1, emit_rlight2, box_01, box_02, box_03, box_04, box_05, box_06, barrel_01, barrel_02, barrel_03, emit_sparkles1, emit_sparkles2, emit_sparkles3;

static-var  statechg_DesiredState, statechg_StateChanging, isbuilding, structureheight, inground, sparklepoint;

// Signal definitions
#define SIG_ACTIVATE			2
#define BOOM		1024+0
#define STRUCTUREBUILDING		1025+0
#define SPARKLES				1026+0
#define DUST				1027+0

lua_CreateLightMe(arg) { return (0); }
lua_DestroyLightMe(arg) { return (0); }

BuildingAnimation()
{
	while (TRUE)
	{
		if( isbuilding == 1 )
		{
			call-script lua_CreateLightMe(createpoint, 0, 255, 255, 96);
			if (sparklepoint == 0) {	emit-sfx SPARKLES from emit_sparkles1;	}
			if (sparklepoint == 1) {	emit-sfx SPARKLES from emit_sparkles2;	}
			if (sparklepoint == 2) {	emit-sfx SPARKLES from emit_sparkles3;	}
			sparklepoint = sparklepoint+1;
			if (sparklepoint > 2) {	sparklepoint = 0;	}
			emit-sfx DUST from createpoint;	
		}
		if( isbuilding == 0 )
		{
			call-script lua_DestroyLightMe();
		}
		sleep 400;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

OpenYard()
{
	set YARD_OPEN to 1;
	while( !get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 1;
	}
	set BUGGER_OFF to 0;
}

CloseYard()
{
	set YARD_OPEN to 0;
	while( get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 0;
	}
	set BUGGER_OFF to 0;
}

Go()
{
	call-script OpenYard();
	set INBUILDSTANCE to 1;
	set ACTIVATION to 1;
}

Stop()
{
	set INBUILDSTANCE to 0;
	set ACTIVATION to 0;	
	call-script CloseYard();
	isbuilding = 0;
	sleep 200;
}

InitState()
{
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == 0 )
		{
			call-script Go();
			currentstate = 0;
		}
		if( statechg_DesiredState == 1 )
		{
			call-script Stop();
			currentstate = 1;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	call-script InitState();
	//////////////////////////////////START BUILD CYCLE
	structureheight	= (get UNIT_HEIGHT)*(-1);
	sparklepoint = 0;
	move base to y-axis structureheight now;
	while( get BUILD_PERCENT_LEFT )
	{
		move base to y-axis (structureheight * (get BUILD_PERCENT_LEFT)/100) speed [10];
		emit-sfx STRUCTUREBUILDING from base;
		sleep 200;
	}
	move base to y-axis 0 speed [10];
	/////////////////////////////////END BUILD CYCLE

	start-script SmokeUnit();
	start-script BuildingAnimation();
}

QueryNanoPiece(piecenum)
{	piecenum = createpoint;	}

Activate()
{
	signal SIG_ACTIVATE;
	start-script RequestState(0);
}

Deactivate()
{
	signal SIG_ACTIVATE;
	set-signal-mask SIG_ACTIVATE;
	set-signal-mask 0;
	start-script RequestState(1);
}

StartBuilding()
{	
	isbuilding = 1;
}

StopBuilding()
{
	isbuilding = 0;
	sleep 200;
}

QueryBuildInfo(piecenum)
{	piecenum = createpoint;	}

SweetSpot(piecenum)
{	piecenum = createpoint;	}

Killed(severity, corpsetype, smoketype)
{
	if (severity <= 50)
	{
		corpsetype = 1;
		emit-sfx BOOM from platform;
		return (corpsetype);
	}
	else
	{
		corpsetype = 2;
		emit-sfx BOOM from base;
		emit-sfx BOOM from platform;
		return (corpsetype);
	}
	return(0);
}
