#include "basic_spring_stuff.h"

piece  base, altar, chest1, chest2, girl, horn1, horn2, horn3, horn4, horn5, horn6, horn7, horn8, horn9, skull1, skull2, skull3, skull4, skullsmall1, skullsmall2, buildpoint, rotator, emit1, emit2;

static-var  statechg_DesiredState, statechg_StateChanging, isbuilding, structureheight, inground;

// Signal definitions
#define SIG_ACTIVATE			2
#define MAGICSPARKLE			1024+0
#define Medium_Explosion_Green	1025+0
#define STRUCTUREBUILDING		1026+0

BuildingAnimation()
{
	while (TRUE)
	{
		if( isbuilding == 1 )
		{
			emit-sfx MAGICSPARKLE from buildpoint;
			emit-sfx MAGICSPARKLE from emit1;
			emit-sfx MAGICSPARKLE from emit2;
			sleep 50;
		}
	sleep 10;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

OpenYard()
{
	set YARD_OPEN to 1;
	while( !get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 1;
	}
	set BUGGER_OFF to 0;
}

CloseYard()
{
	set YARD_OPEN to 0;
	while( get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 0;
	}
	set BUGGER_OFF to 0;
}

Go()
{
	call-script OpenYard();
	set INBUILDSTANCE to 1;
}

Stop()
{
	set INBUILDSTANCE to 0;
	call-script CloseYard();
	sleep 200;
}

InitState()
{
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == 0 )
		{
			call-script Go();
			currentstate = 0;
		}
		if( statechg_DesiredState == 1 )
		{
			call-script Stop();
			currentstate = 1;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	//////////////////////////////////START BUILD CYCLE
	hide horn6;
	hide horn7;
	hide horn8;
	hide horn9;
	structureheight	= (get UNIT_HEIGHT)*(-1);
	move altar to y-axis structureheight now;
	call-script InitState();
	while( get BUILD_PERCENT_LEFT )
	{
		move altar to y-axis (structureheight * (get BUILD_PERCENT_LEFT)/100) speed [10];
		emit-sfx STRUCTUREBUILDING from base;
		sleep 50;
	}
	move altar to y-axis 0 speed [10];
	/////////////////////////////////END BUILD CYCLE

	spin rotator around x-axis speed <150> accelerate <70>;
	spin rotator around y-axis speed <100> accelerate <100>;


	start-script SmokeUnit();
	start-script BuildingAnimation();
}

QueryNanoPiece(piecenum)
{	piecenum = base;	}

Activate()
{
	signal SIG_ACTIVATE;
	start-script RequestState(0);
}

Deactivate()
{
	signal SIG_ACTIVATE;
	set-signal-mask SIG_ACTIVATE;
	set-signal-mask 0;
	start-script RequestState(1);
}

StartBuilding()
{	
	sleep 300;
	isbuilding = 1;
}

StopBuilding()
{
	isbuilding = 0;
	sleep 300;
}

QueryBuildInfo(piecenum)
{	piecenum = buildpoint;	}

SweetSpot(piecenum)
{	piecenum = base;	}

Killed(severity, corpsetype, smoketype)
{
	if (severity <= 50)
	{
		corpsetype = 1;
		emit-sfx Medium_Explosion_Green from altar;
		return (corpsetype);
	}
	else
	{
		corpsetype = 2;
		emit-sfx Medium_Explosion_Green from altar;
		return (corpsetype);
	}
	return(0);
}
